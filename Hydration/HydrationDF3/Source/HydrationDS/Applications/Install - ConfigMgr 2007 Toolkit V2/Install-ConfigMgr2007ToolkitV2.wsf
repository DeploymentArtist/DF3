<job id="Install-ConfigMgr2007ToolkitV2">
<script language="VBScript" src="..\..\Scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

'//----------------------------------------------------------------------------
'// Solution: Hydration
'// Purpose: Used to install Install-ConfigMgr2007ToolkitV2
'// Usage: cscript Install-ConfigMgr2007ToolkitV2.wsf [/debug:true]
'// Version: 1.2 - 27 Dec 2010 - Johan Arwidmark
'//
'// This script is provided "AS IS" with no warranties, confers no rights and 
'// is not supported by the authors or Deployment Artist. 
'//
'//----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declarations
'//---------------------------------------------------------------------------- 

Option Explicit 

Dim iRetVal 

'//----------------------------------------------------------------------------
'// End declarations
'//---------------------------------------------------------------------------- 

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 

On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'//
'// Function: ZTIProcess()
'//
'// Input: None
'// 
'// Return: Success - 0
'// Failure - non-zero
'//
'// Purpose: Perform main ZTI processing
'// 
'//---------------------------------------------------------------------------
Function ZTIProcess() 

	oLogging.CreateEntry "Install ConfigMgr 2007 Toolkit V2: Starting ConfigMgr Toolkit installation", LogTypeInfo	

	' Disable Zone Checks
	oEnv("SEE_MASK_NOZONECHECKS") = 1 

	' Install Toolkit
	iRetVal = oUtility.RunWithHeartbeat("msiexec /i """ & oUtility.ScriptDir & "\Source\ConfigMgrTools.msi""" & " /qb-! /l*v """ & oUtility.LocalRootPath & "\SMSOSD\OSDLOGS\Install-ConfigMgr2007ToolkitV2-Application.log""")
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
	Else 
		ZTIProcess = Failure
		oLogging.CreateEntry "Install ConfigMgr 2007 Toolkit V2: Error installing ConfigMgr 2007 Toolkit V2. Check the log " & oUtility.LocalRootPath & "\SMSOSD\OSDLOGS\Install-ConfigMgr2007ToolkitV2-Application", LogTypeWarning
	End If
	
	' Enable Zone Checks
	oEnv.Remove("SEE_MASK_NOZONECHECKS")

    ' Register .log files with Trace32
    oShell.RegWrite "HKEY_CLASSES_ROOT\Log.File\shell\open\command\", """C:\Program Files (x86)\ConfigMgr 2007 Toolkit V2\Trace32.exe"" ""%1""", "REG_SZ"
	oShell.RegWrite "HKEY_CLASSES_ROOT\.log\", "Log.File", "REG_SZ"
	oShell.RegWrite "HKEY_CURRENT_USER\Software\Microsoft\Trace32\Register File Types", "0", "REG_SZ"


	oLogging.CreateEntry "Install ConfigMgr 2007 Toolkit V2: Return code from command = " & iRetVal, LogTypeInfo
	oLogging.CreateEntry "Install ConfigMgr 2007 Toolkit V2: Finished ConfigMgr 2007 Toolkit V2 installation", LogTypeInfo	

End Function 

</script>
</job>















